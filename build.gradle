plugins {
    id 'application'
    id 'com.github.spotbugs' version '4.7.3'
    id 'checkstyle'
}

group = 'com.dmst.fork'
version = '1.0'

application {
    mainClass = 'fork.Main'
}

//Set Main class for the jar file
jar {
    manifest {
        attributes 'Main-Class': 'fork.Main'
    }
    //Add dependencies to the jar file
    from {
        configurations.compileClasspath.collect {it.isDirectory()? it :zipTree(it)}
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.30.1'
    testImplementation 'junit:junit:4.13.2'
}

test {
    useJUnit()
}

spotbugs {
    // Configure spotbugs not to fail the build if it finds violations
    ignoreFailures = true
    // Suppress spotbugs's stacktrace
    showStackTraces = false
    // Maximize the analysis effort level
    effort = 'max'
    // Set the priority threshold for reporting bugs to medium 
    reportLevel = 'medium'
}

// Configure SpotbugsMain to create html reports
spotbugsMain {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

// Configure SpotbugsTest to create html reports
spotbugsTest {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

checkstyle {
    toolVersion = '8.29'
}